# Day 17: Day 17: Spinlock

This was an interesting puzzle to solve in Pharo.  My solution to Part 1 is object oriented, with an object to model the buffer described in the puzzle.  My solution to Part 2, however, is not object oriented because it is much faster to compute the solution rather than simulate it.

## Solution to Part 1

First we model the buffer described in the puzzle.  Since we are going to have to add elements in the middle of the buffer, I used a linked list to represent the values in the buffer.  The method `spin` performs the basic operation described in the puzzle: moving the current position, inserting a new value, and then moving the current position to the newly inserted value.

```smalltalk
Day17SpinlockBuffer >> spin
	| actualSteps |
	actualSteps := self steps rem: self nextIndex.
	1 to: actualSteps do: [ :i | self step ].
	self currentPos: (self buffer add: nextIndex afterLink: currentPos).
	self nextIndex: self nextIndex + 1
```

There is an auxiliary method that handles wrapping around to the beginning of the buffer.

```smalltalk
Day17SpinlockBuffer >> step
	self currentPos nextLink = nil
		ifTrue: [ self currentPos: self buffer firstLink ]
		ifFalse: [ self currentPos: self currentPos nextLink ]
```

Now we can use the `Day17SpinlockBuffer` to simulate our puzzle to get the answer.

```smalltalk
Day17Solver >> solveA: anInput
	| buffer |
	buffer := Day17SpinlockBuffer newWithSteps: anInput.
	1 to: 2017 do: [ :i | buffer spin ].
	^ buffer currentPos nextLink value
```

## Solution to Part 2

Attempting to simulate 50000000 spins, complete with inserting elements, would be unwieldy.  My original Python solution simulated the 50000000 spins but only kept track of the when the value after 0 changed.  This solution, however, is slow.

The solution below makes use of an optimization where you compute the number of spins/steps that could be taken without wrapping around to the beginning of the buffer.  You can then take _all_ those steps at once, speeding up the computation significantly.

```smalltalk
Day17Solver >> solveB: anInput
	| index curr value limit |
	index := 0.
	curr := 1.
	value := 0.
	limit := 50000000.
	[ curr <= limit ]
		whileTrue: [ | jump |
			jump := (curr - index - 1) // anInput.
			jump > 0
				ifTrue: [ index := index + (jump * anInput) + jump.
					curr := curr + jump ]
				ifFalse: [ | actualSteps |
					actualSteps := anInput rem: curr.
					index := index + actualSteps rem: curr.
					index = 0
						ifTrue: [ value := curr ].
					curr := curr + 1.
					index := index + 1 ] ].
	^ value
```