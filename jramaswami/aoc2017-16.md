# Day 16: Permutation Promenade


This puzzle proved tricky for me to solve in Pharo.  See below for a description of my new to Pharo difficulties.

## Solution to Part 1

The first thing I did was model the dancers as a class so I could add all the operations necessary to perform a dance.

```smalltalk
Day16Dancers >> spin: anInteger
	| hd tl i |
	i := dancers size - anInteger.
	hd := dancers copyFrom: i + 1 to: dancers size.
	tl := dancers copyFrom: 1 to: i.
	self dancers: hd , tl
```

```smalltalk
Day16Dancers >> exchange: aPosition1 with: aPosition2
	| t |
	t := dancers at: aPosition1.
	self dancers at: aPosition1 put: (self dancers at: aPosition2).
	dancers at: aPosition2 put: t
```

```smalltalk
Day16Dancers >> partner: aPartner1 with: aPartner2
	| s t |
	s := dancers indexOf: aPartner1.
	t := dancers indexOf: aPartner2.
	dancers at: s put: aPartner2.
	dancers at: t put: aPartner1
```

Now that I have the operations working, I need to parse the dance instructions.  At first, I parsed them into block closures that could be applied.  That didn't seem to work (see below) so I changed my strategy to just parse the instructions into lines and then use a method to perform each instruction as a string.

```smalltalk
Day16Dancers >> parseDance: anInput
	self danceInstructions: (anInput findTokens: $,)
```

```smalltalk
Day16Dancers >> doInstruction: anInstruction
	(anInstruction at: 1) = $s
		ifTrue: [ | spinArg |
			spinArg := (anInstruction copyFrom: 2 to: anInstruction size) asInteger.
			self spin: spinArg ].
	(anInstruction at: 1) = $x
		ifTrue: [ | i a b |
			i := anInstruction indexOf: $/.
			"Add one to a, b to translate from 0 based indexing to 1 based indexing."
			a := 1 + (anInstruction copyFrom: 2 to: i - 1) asInteger.
			b := 1 + (anInstruction copyFrom: i + 1 to: anInstruction size) asInteger.
			self exchange: a with: b ].
	^ (anInstruction at: 1) = $p
		ifTrue: [ | a b |
			a := anInstruction at: 2.
			b := anInstruction at: 4.
			self partner: a with: b ]
```

The whole dance is performed using the `dance` method:

```smalltalk
Day16Dancers >> dance
	self danceInstructions do: [ :instruction | self doInstruction: instruction ]
```

Once we have our dancers modeled, we can solve the first part of the puzzle by just running through one round of the dance and returning the dancers as a string.

```smalltalk
Day16Solver >> solveAWithDancers: aString andADance: anInput
	| dancers |
	dancers := Day16Dancers newFromDancersString: aString withDanceString: anInput.
	dancers dance.
	^ dancers dancers
```

## Solution to Part 2

The solution to part 2 involves finding a cycle within the dance.  To do this, I run the dancers through their dance repeatedly until I find a repeated dance state.  By recording the time of each dance state we can then compute what dance state is our solution.

```smalltalk
Day16Solver >> solveBWithDancers: aString andADance: anInput
	| dancers danceStates time result |
	time := 0.
	danceStates := Dictionary new.
	dancers := Day16Dancers newFromDancersString: aString withDanceString: anInput.
	danceStates at: (dancers dancers copyFrom: 1 to: 16) put: time.
	time := time + 1.
	dancers dance.
	[ danceStates includesKey: dancers dancers ]
		whileFalse: [ danceStates at: (dancers dancers copyFrom: 1 to: 16) put: time.
			time := time + 1.
			dancers dance ].
	result := 1000000000 rem: (danceStates size).
	^ danceStates keyAtValue: result.
```

## Python to Pharo Leads to Trouble

This is the code that I used to solve the puzzle on my first try:

```smalltalk
Day16Dancers >> parseInstruction: anInstruction
	(anInstruction at: 1) = $s
		ifTrue: [ | spinArg |
			spinArg := (anInstruction copyFrom: 2 to: anInstruction size) asInteger.
			^ [ self spin: spinArg ] ].
	(anInstruction at: 1) = $x
		ifTrue: [ | i a b |
			i := anInstruction indexOf: $/.
			"Add one to a, b to translate from 0 based indexing to 1 based indexing."
			a := 1 +(anInstruction copyFrom: 2 to: i - 1) asInteger.
			b := 1 + (anInstruction copyFrom: i + 1 to: anInstruction size) asInteger.
			^ [ self exchange: a with: b ] ].
	^ (anInstruction at: 1) = $p
		ifTrue: [ | a b |
			a := anInstruction at: 2.
			b := anInstruction at: 4.
			^ [ self partner: a with: b ] ]
```

However, it behaved very strangely.  It would pass all my tests and give the right answer.  But if I ran the test a second time, it would fail.  A third time, it would pass.  I thought the problem was that, for some reason, the first instruction of my input was getting applied immediately, leaving me with an incorrect starting position.  At first I thought it was something wrong with my code (maybe I shouldn't be using fancy BlockClosures) or perhaps even a bug in Pharo.  However, when I changed my solution to avoid using blocks, I still had the same problem.  I think the problem was the use of a string as the dancers.  As a python programmer I am used to immutable strings.  I don't think this is the case in Pharo.  I think the string in my data class was mutating and that was causing the problem.  When I started returning a copy of the string from the data class, the code started to behave as expected.  I ran into the same problem solving the second part of the puzzle.  Python programmers use strings as dictionary keys all the time; but if your strings are mutable ... it won't work.  Using copies fixed the problem again.