# Day 24: Electromagnetic Moat

To solve the day 24 puzzle I modeled the moat as a graph.  This allowed me to do use a depth first search algorithm to solve the puzzle.

First we have to parse the input into a graph.

```smalltalk
Day24Graph >> parseGraph: anInput
	anInput
		linesDo: [ :line | 
			| tokens u v |
			tokens := line findTokens: $/.
			u := (tokens at: 1) asInteger.
			v := (tokens at: 2) asInteger.
			self addEdgeBetween: u and: v ]
```

This method calls the `addEdgeBetween` method for every line of the input to add our edges to the graph.

```smalltalk
Day24Graph >> addEdgeBetween: anIntegerU and: anIntegerV
	| edge |
	edge := Day24Edge newFrom: anIntegerU to: anIntegerV.
	(self adj includesKey: anIntegerU) ifFalse: [ self adj at: anIntegerU put: OrderedCollection new ].
	(self adj includesKey: anIntegerV) ifFalse: [ self adj at: anIntegerV put: OrderedCollection new ].
	(self adj at: anIntegerU) add: edge.
	(self adj at: anIntegerV) add: edge.
```

We add two edges for each line of input because the graph is undirected.  Once we have the input as a graph we can solve the puzzle.

The solution to both parts uses the same depht first search method.

```smalltalk 
Day24Graph >> dfs: aNode score: aScore depth: aDepth
	self strongestBridge: (self strongestBridge max: aScore).
	aDepth > self longestBridge
		ifTrue: [ self longestBridge: aDepth.
			self longestBridgeScore: aScore ].
	aDepth = self longestBridge
		ifTrue: [ self longestBridgeScore: (self longestBridgeScore max: aScore) ].
	(self adj at: aNode)
		do: [ :edge | 
			edge visited
				ifFalse: [ | node_v e_score |
					edge visit.
					node_v := edge other: aNode.
					e_score := edge score.
					self dfs: node_v score: aScore + e_score depth: aDepth + 1.
					edge unvisit ] ]
```

For the first part of the puzzle we keep track of the strongest bridge, i.e. the maximum score.  For the second part we keep track of the longest bridge, i.e. the maximum depth, and the maximum score for the bridges of the given maximum length.

## Solution to Part 1

Here is the method from the solver for part 1.

```smalltalk
Day24Solver >> solveA: anInput
	| graph |
	graph := Day24Graph new.
	graph parseGraph: anInput.
	graph dfs: 0 score: 0 depth: 0.
	^ graph strongestBridge
```

## Solution to Part 2

Here is the method from the solver for part 2.

```smalltalk
Day24Solver >> solveB: anInput
	| graph |
	graph := Day24Graph new.
	graph parseGraph: anInput.
	graph dfs: 0 score: 0 depth: 0.
	^ graph longestBridgeScore
```